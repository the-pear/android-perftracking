apply plugin: 'com.android.library'

android {
    compileSdkVersion CONFIG.versions.android.sdk.compile
    buildToolsVersion CONFIG.versions.android.tools

    defaultConfig {
        minSdkVersion CONFIG.versions.android.sdk.min
        targetSdkVersion CONFIG.versions.android.sdk.target
        versionName project.MODULE_VERSION
        resValue 'string', 'perftracking__version', project.MODULE_VERSION
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    resourcePrefix 'perftracking_'
}

// --- DEPENDENCIES ---
configurations { copyJar }

task copyJar(type: Copy) {
    from configurations.copyJar
    into 'libs'
}

android.libraryVariants.all {
    project.tasks["prepare${it.buildType.name.capitalize()}Dependencies"].dependsOn copyJar
}

dependencies {
    // --- COMPILE ---
    copyJar project(':Core')

    compile files('libs/Core.jar')
    compile "com.android.support:support-annotations:$CONFIG.versions.android.libraries.support"
    compile 'jp.co.rakuten.api:core:1.2.0'
    compile 'com.rakuten.tech.mobile.relay:relay-util:0.0.1'
}

apply from: "test.gradle"

// -- JAVADOC --

apply plugin: 'jp.co.rakuten.sdtd.javadoc'

javadoc {
    name = project.MODULE_NAME
    version = project.MODULE_VERSION
    title = project.MODULE_DESCRIPTION
    exclude = ['**/Measurement.java']
}


// -- ARTIFACTORY --

apply plugin: 'jp.co.rakuten.sdtd.artifactory'

artifactory {
    group =         project.MODULE_GROUP
    artifact =      project.MODULE_NAME
    version =       project.MODULE_VERSION
    description =   project.MODULE_DESCRIPTION
    sourceUrl =     project.MODULE_REPOSITORY
}

// -- LOCAL MAVEN --

apply plugin: 'maven-publish'
publishing {
    publications {
        local(MavenPublication) {
            groupId     project.MODULE_GROUP
            artifactId  project.MODULE_NAME
            version     project.MODULE_VERSION
            artifact("$buildDir/outputs/aar/Runtime-release.aar")
        }
    }
}

tasks.publishToMavenLocal.dependsOn(tasks.assemble)