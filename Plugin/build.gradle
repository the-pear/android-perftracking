apply plugin: 'groovy'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url ARTIFACTORY_RELEASE
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'jp.co.rakuten.sdtd.plugin:artifactory:1.3.0'
    }
}

// Quick-and-dirty way of adding both Stubs and Core without dealing with jarsâ€¦
sourceSets {
    main {
        java {
            srcDirs += "$rootProject.projectDir/Stubs/src/main/java"
            srcDirs += "$rootProject.projectDir/Core/src/main/java"
        }
    }
}

// Inject the version number into the resources
import org.apache.tools.ant.filters.*
afterEvaluate {
    configure(allProcessResourcesTasks()) {
        filter(ReplaceTokens, tokens: [
            version:    project.MODULE_VERSION,
            runtime:    "com.rakuten.tech.mobile.perf:runtime:$project.MODULE_VERSION".toString(),
            repository: project.ARTIFACTORY_RELEASE
        ])
    }
}
def allProcessResourcesTasks() {
    sourceSets*.processResourcesTaskName.collect { tasks[it] }
}

dependencies {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url ARTIFACTORY_RELEASE
        }
        maven {
            url ARTIFACTORY_SNAPSHOT
        }
    }

    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.2.2'
    compile 'org.ow2.asm:asm-all:5.1'
}

// -- ARTIFACTORY --

apply plugin: 'jp.co.rakuten.sdtd.artifactory'

artifactory {
    group = project.MODULE_GROUP
    artifact = project.MODULE_NAME
    version = project.MODULE_VERSION
    description = project.MODULE_DESCRIPTION
    sourceUrl = project.MODULE_REPOSITORY
}

// -- LOCAL MAVEN --

apply plugin: 'maven-publish'
publishing {
    publications {
        local(MavenPublication) {
            groupId     project.MODULE_GROUP
            artifactId  project.MODULE_NAME
            version     project.MODULE_VERSION
            artifact("$buildDir/libs/Plugin-${project.MODULE_VERSION}.jar")
        }
    }
}

tasks.publishToMavenLocal.dependsOn(tasks.assemble)